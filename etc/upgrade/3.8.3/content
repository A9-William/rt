@ScripConditions = (
    {  Name                 => 'On Reject',                                # loc
       Description          => 'Whenever a ticket is rejected',            # loc
       ApplicableTransTypes => 'Status',
       ExecModule           => 'StatusChange',
       Argument             => 'rejected'

    },
);


@Final = (
    sub {
        $RT::Logger->debug("Going to canonicalize ACLs in the DB");
        my $sys = RT::System->new($RT::SystemUser);

        my $acl = RT::ACL->new( $RT::SystemUser );
        $acl->UnLimit;
        while ( my $ace = $acl->Next ) {
            my $old = $ace->__Value('RightName');
            my $new = $old;
            $new =~ s/s$//i;
            next if $new eq $old;

            my ($status, $msg) = $ace->__Set( Field => 'RightName', Value => $new );
            $RT::Logger->error( "Couldn't change right name: $msg" )
                unless $status;
        }

        $RT::Logger->debug("Canonicalized ACLs in the DB.");
        return 1;
    },
    sub {
        $RT::Logger->debug("Going to correct descriptions of notify actions in the DB");
        my $sys = RT::System->new($RT::SystemUser);

        my $actions = RT::ScripActions->new( $RT::SystemUser );
        $actions->Limit(
            FIELD => 'ExecModule',
            VALUE => 'Notify',
        );
        $actions->Limit(
            FIELD => 'Argument',
            VALUE => 'All',
        );
        while ( my $action = $actions->Next ) {
            my ($status, $msg) = $action->__Set( Field => 'Name', Value => 'Notify Owner, Requestors, Ccs and AdminCcs' );
            $RT::Logger->warning( "Couldn't change action name: $msg" )
                unless $status;

            ($status, $msg) = $action->__Set( Field => 'Description', Value => 'Send mail to owner and all watchers' );
            $RT::Logger->warning( "Couldn't change action description: $msg" )
                unless $status;
        }

        $actions = RT::ScripActions->new( $RT::SystemUser );
        $actions->Limit(
            FIELD => 'ExecModule',
            VALUE => 'NotifyAsComment',
        );
        $actions->Limit(
            FIELD => 'Argument',
            VALUE => 'All',
        );
        while ( my $action = $actions->Next ) {
            my ($status, $msg) = $action->__Set( Field => 'Name', Value => 'Notify Owner, Requestors, Ccs and AdminCcs as Comment' );
            $RT::Logger->warning( "Couldn't change action name: $msg" )
                unless $status;

            ($status, $msg) = $action->__Set( Field => 'Description', Value => 'Send mail to owner and all watchers as a "comment"' );
            $RT::Logger->warning( "Couldn't change action description: $msg" )
                unless $status;
        }

        $RT::Logger->debug("Corrected descriptions of notify actions in the DB.");
        return 1;
    },
);



