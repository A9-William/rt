@Final = (
    sub {
        $RT::Logger->debug("Going to adjust dashboards");
        my $sys = RT::System->new($RT::SystemUser);

        my $attrs = RT::Attributes->new( $RT::SystemUser );
        $attrs->Named('Dashboard');

        if ($attrs->Count == 0) {
            $RT::Logger->debug("You have no dashboards. Skipped.");
            return 1;
        }

        while (my $attr = $attrs->Next) {
            my $props = $attr->Content;
            if (exists $props->{Searches}) {
                $props->{Portlets} = [ map {
                    my ($privacy, $id, $desc) = @$_;

                    {
                        portlet_type => 'search',
                        privacy      => $privacy,
                        id           => $id,
                        description  => $desc,

                    }
                } @{ delete $props->{Searches} } ];
            }
            my ($status, $msg) = $attr->SetContent( $props );
            $RT::Logger->error($msg) unless $status;
        }

        $RT::Logger->debug("Fixed.");
        return 1;
    },
);
