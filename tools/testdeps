#!/usr/bin/perl -w

# $Header$

# Copyright 2000 Jesse Vincent <jesse@fsck.com>
# Distributed under the GNU General Public License
# 

#
# This is just a basic script that checks to make sure that all
# the modules needed by RT before you can install it.
#

use strict;

use vars qw($mode $module @modules);

$mode = shift || &print_help;

@modules = qw(
DBI 1.13
DBIx::DataSource 0.02
DBIx::DBSchema 0.14
DBIx::SearchBuilder 0.18
HTML::Entities 
HTML::Mason 0.895
CGI::Cookie 1.06
Apache::Session 1.53
Date::Manip 
Date::Format 
MIME::Entity 5.108
Mail::Mailer 1.20
Getopt::Long 
Tie::IxHash 
Text::Wrapper 
Text::Template
Log::Dispatch 1.6
);
use CPAN;

while ($module= shift @modules) {
	my $version = "";
	$version = " ". shift (@modules) . " " if ($modules[0] =~ /^([\d\.]*)$/);
	print "Checking for $module$version";
	eval "use $module$version" ;
	if ($@) {
	&resolve_dependency($module, $version) 
	}
	else {
	print "...found\n";
	}
}

sub print_help {
print <<EOF;

$0 is a tool for RT that will tell you if you've got all
the modules RT depends on properly installed.

Flags: (only one flag is valid for a given run)

-quiet will check to see if we've got everything we need
	and will exit with a return code of (1) if we don't.

-warn will tell you what isn't properly installed

-fix will use CPAN to magically make everything better

EOF
}

sub resolve_dependency {
	my $module = shift;
	my $version = shift;
        print "....$module$version not installed.";
    if ($mode =~ /-f/) {
        print "Installing with CPAN...";
        CPAN::install($module);
     }
     print "\n";
	exit(1) if ($mode =~ /-q/);
}	
	
