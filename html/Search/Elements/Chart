<%args>
$Query => "id > 0"
$PrimaryGroupBy => 'Queue'
$SecondaryGroupBy => undef
$ChartStyle => 'bars'
</%args>
<%init>
use RT::Report::Tickets;
my $tix = RT::Report::Tickets->new( $session{'CurrentUser'} );
$tix->FromSQL( $Query );
my $count_name = $tix->Column( FUNCTION => 'COUNT', FIELD => 'id' );
$tix->GroupBy( FIELD => $PrimaryGroupBy );
my $value_name = $tix->Column( FIELD => $PrimaryGroupBy );

my %class = (
    Queue => 'RT::Queue',
    Owner => 'RT::User',
);
my $class = $class{ $PrimaryGroupBy };

my (@keys, @values);
while ( my $entry = $tix->Next ) {
    if ($class) {
        my $q = $class->new( $session{'CurrentUser'} );
        $q->Load( $entry->__Value( $value_name ) );
        push @keys, $q->Name;
    }
    else {
        push @keys, $entry->__Value( $value_name );
    }
    $keys[-1] ||= loc('(no value)');
    push @values, $entry->__Value( $count_name );
}

# XXX: Convert 1970-01-01 date to the 'Not Set'
# this code should be generalized!!!
if ( $PrimaryGroupBy =~ /(Daily|Monthly|Annually)$/ ) {
    my $re;
    $re = qr{1970-01-01} if $PrimaryGroupBy =~ /Daily$/;
    $re = qr{1970-01} if $PrimaryGroupBy =~ /Monthly$/;
    $re = qr{1970} if $PrimaryGroupBy =~ /Annually$/;
    foreach (@keys) {
        s/^$re/loc('Not Set')/e;
    }   
}


my $query_string = $m->comp('/Elements/QueryString', %ARGS);
</%init>

<% loc('Query:') %>&nbsp;<% $Query %><br />

<img src="<%RT->Config->Get('WebPath')%>/Search/Chart?<%$query_string|n%>" /><br />

<table class="collection-as-table">
<tr>
<th class="collection-as-table"><% $tix->Label($PrimaryGroupBy) %>
</th>
<th class="collection-as-table"><&|/l&>Tickets</&>
</th>
</tr>
% my ($i,$total);
% while (my $key = shift @keys) {
% $i++;
% my $value = shift @values;
% $total += $value;
<tr class="<%$i%2 ? 'evenline' : 'oddline' %>">
<td class="label collection-as-table">
<%$key%>
</td>
<td class="value collection-as-table">
<%$value%>
</td>
</tr>
% }

%$i++;
<tr class="<%$i%2 ? 'evenline' : 'oddline' %>">
<td class="label collection-as-table">
<%loc('Total')%>
</td>
<td class="value collection-as-table">
<%$total%>
</td>
</tr>

</table>
