%# BEGIN LICENSE BLOCK
%# 
%#  Copyright (c) 2002-2003 Jesse Vincent <jesse@bestpractical.com>
%#  
%#  This program is free software; you can redistribute it and/or modify
%#  it under the terms of version 2 of the GNU General Public License 
%#  as published by the Free Software Foundation.
%# 
%#  A copy of that license should have arrived with this
%#  software, but in any event can be snarfed from www.gnu.org.
%# 
%#  This program is distributed in the hope that it will be useful,
%#  but WITHOUT ANY WARRANTY; without even the implied warranty of
%#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
%#  GNU General Public License for more details.
%# 
%# END LICENSE BLOCK

<& /RTFM/Article/Elements/Tabs, current_tab => "RTFM/Article/Search.html", Title => loc("Search for articles") &>
<& /Elements/ListActions, actions => \@results &>
<form action="Search.html" method="GET">
<& Elements/ShowSearchCriteria, dates => \%dates, RefersTo => $RefersTo, customfields => $customfields,  ReferredToBy => $ReferredToBy, %ARGS &>
<br />
<br />
<& Elements/ShowSavedSearches, CurrentSearch => $CurrentSearch, 
    Name => ($search ? $search->Name : undef),
    Privacy => ($search ? $search->Privacy : undef) &>

</form>
<br />
<br />
<hr />
<br />
<& Elements/ShowSearchResults, articles => $articles &>
<div align=right>
<a href="<%$RT::WebPath%>/RTFM/Article/Search.html<%$QueryString%>"><&|/l&>Bookmarkable link</&></a><br>
</div>
<%init>
use RT::SavedSearch;
my @results;
my $articles = RT::FM::ArticleCollection->new( $session{'CurrentUser'} );

# {{{ Quicksearch logic

# If it is a number, load the article with that ID.  Otherwise, search
# on name and summary.
if ($ARGS{'q'} =~ /^(\d+)$/) {
    $m->comp("/RTFM/Article/Display.html", id => $1);
    return;
} elsif ($ARGS{'q'}) {
    $articles->Limit( FIELD => 'Name',
		      SUBCLAUSE => 'NameOrSummary',
		      OPERATOR => 'LIKE',
		      ENTRYAGGREGATOR => 'OR',
		      VALUE => $ARGS{'q'} );
    $articles->Limit( FIELD => 'Summary',
		      SUBCLAUSE => 'NameOrSummary',
		      OPERATOR => 'LIKE',
		      ENTRYAGGREGATOR => 'OR',
		      VALUE => $ARGS{'q'} );
}

# }}}

# {{{ Saved search logic

my $search;

# The keys in %ARGS that are not saved and loaded with named searches.
# These need to be treated specially.
my @metakeys = qw/NewSearchName CurrentSearch SearchPrivacy Save Load
    Update Delete/;

if ($CurrentSearch =~ /^(.*-\d+)-SavedSearch-(\d+)$/) {
    $search = RT::SavedSearch->new($session{'CurrentUser'});
    $search->Load($1, $2);
}

# Have we been asked to load a search?

if ($ARGS{'Load'}) {
    if ($ARGS{'LoadSavedSearch'} =~ /^(.*-\d+)-SavedSearch-(\d+)$/ ) {
	my $privacy = $1;
	my $search_id = $2;
	
	$search = RT::SavedSearch->new($session{'CurrentUser'});
	my ($ret, $msg) = $search->Load($privacy, $search_id);
	if ($ret) {
	    my $searchargs = $search->GetParameter('args');
	    # Clean out ARGS and fill it in with the saved args from the 
	    # loaded search.
	    foreach my $key (@metakeys) {
		$searchargs->{$key} = $ARGS{$key};
	    }
	    %ARGS = %{$searchargs};
	    $CurrentSearch = "$privacy-SavedSearch-$search_id";
	} else {
	    push(@results, loc("Error: could not load saved search [_1]: [_2]",
			       $ARGS{'LoadSavedSearch'}, $msg));
	}
    } else {
	push(@results, loc("Invalid [_1] argument", 'LoadSavedSearch'));
    }
}

# ...or have we been asked to save, update, or delete a search?

if ($ARGS{'Save'}) {
    my %searchargs = %ARGS;
    foreach my $key (@metakeys) {
	delete $searchargs{$key};
    }

    $search = RT::SavedSearch->new($session{'CurrentUser'});
    unless ($ARGS{'SearchPrivacy'} =~ /^(.*)-(\d+)$/) {
	# This shouldn't really happen, but hey.
	push(@results, loc("WARNING: Saving search to user-level privacy"));
	$ARGS{'SearchPrivacy'} = 'RT::User-'.$session{'CurrentUser'}->Id;
    }
    my ($ret, $msg) = $search->Save(Privacy => $ARGS{'SearchPrivacy'}, 
				    Type => 'Article',
				    Name => $ARGS{'NewSearchName'}, 
				    SearchParams => {'args' => \%searchargs});
    if ($ret) {
	$CurrentSearch = $ARGS{'SearchPrivacy'} . "-SavedSearch-" . 
	    $search->Id;
	push(@results, loc("Created search [_1]", $search->Name));
    } else {
        undef $search; # if we bomb out creating a search
                        # we don't want to have the empty object hang around
	push(@results, loc("Could not create search: [_1]", $msg));
    }
} elsif ($ARGS{'Update'}) {
    if ($ARGS{'SearchPrivacy'} != $search->Privacy) {
	push(@results, 
	     loc("Error: cannot change privacy value of existing search"));
    } else {
	my %searchargs = %ARGS;
	foreach my $key (@metakeys) {
	    delete $searchargs{$key};
	}
	
	# We already have a search loaded, because CurrentSearch is set,
	# or else we would not have gotten here.
	my ($ret, $msg) = $search->Update(Name => $ARGS{'NewSearchName'},
					  SearchParams => \%searchargs);
	if ($ret) {
	    push(@results, loc("Search [_1] updated", $search->Name));
	} else {
	    push(@results, loc("Error: search [_1] not updated: [_2]",
			       $search->Name, $msg));
	}
    }
} elsif ($ARGS{'Delete'}) {
    # Keep track of this, as we are about to delete the search.
    my $searchname = $search->Name;
    my ($ret, $msg) = $search->Delete;
    if ($ret) {
	$ARGS{'CurrentSearch'} = undef;
	push(@results, loc("Deleted search [_1]", $searchname));
	# Get rid of all the state.
	foreach my $key (keys %ARGS) {
	    delete $ARGS{$key};
	}
	$CurrentSearch = 'new';
	$search = undef;
	$RefersTo = undef;
	$ReferredToBy = undef;
    } else {
	push(@results, loc("Could not delete search [_1]: [_2]", 
	     $searchname, $msg));
    }
}

# }}}


# Don't want to search for a null class when there is no class specced
my $customfields = RT::CustomFields->new( $session{'CurrentUser'} );
$customfields->LimitToLookupType(RT::FM::Article->new($session{'CurrentUser'})->CustomFieldLookupType);
if ( $ARGS{'Class'} ) {
    my @Classes =
      ( ref $ARGS{'Class'} eq 'ARRAY' )
      ? @{ $ARGS{'Class'} }
      : ( $ARGS{'Class'} );
    foreach my $class (@Classes) {
        $customfields->LimitToGlobalOrObjectId($class);
    }
}
else {
    $customfields->LimitToGlobalOrObjectId();
}

my %dates;
foreach my $date qw(Created< Created> LastUpdated< LastUpdated>) {
    next unless ( $ARGS{$date} );
    my $seconds = parsedate( $ARGS{$date}, FUZZY => 1, PREFER_PAST => 1 );
    my $date_obj = RT::Date->new( $session{'CurrentUser'} );
    $date_obj->Set( Format => 'unix', Value => $seconds );
    $dates{$date} = $date_obj;

    if ( $date =~ /^(.*?)<$/i ) {
        $articles->Limit( FIELD           => $1, OPERATOR        => "<=", ENTRYAGGREGATOR => "AND", VALUE           => $date_obj->ISO );
    }

    if ( $date =~ /^(.*?)>$/i ) {
        $articles->Limit( FIELD           => $1, OPERATOR        => ">=", ENTRYAGGREGATOR => "AND", VALUE           => $date_obj->ISO );
    }

}
foreach my $link ( split ( /\s+/, $RefersTo ) ) {
    next unless ($link);
    $articles->LimitRefersTo($link);
}

foreach my $link ( split ( /\s+/, $ReferredToBy ) ) {
    next unless ($link);
    $articles->LimitReferredToBy($link);
}

if ($ARGS{'Topics'}) {
    my @Topics =
      ( ref $ARGS{'Topics'} eq 'ARRAY' )
      ? @{ $ARGS{'Topics'} }
      : ( $ARGS{'Topics'} );
    @Topics = map {split} @Topics;
    if ($ARGS{'ExpandTopics'}) {
        my %topics;
        while (@Topics) {
            my $id = shift @Topics;
            next if $topics{$id};
            my $Topics = RT::FM::TopicCollection->new($session{'CurrentUser'});
            $Topics->Limit(FIELD => 'Parent', VALUE => $id);
            push @Topics, $_->Id while $_ = $Topics->Next;
            $topics{$id}++;
        }
        @Topics = keys %topics;
        $ARGS{'Topics'} = \@Topics;
    }
    $articles->LimitTopics(@Topics);
}

my %cfs;
my $all_cfs = RT::CustomFields->new($session{'CurrentUser'});
$all_cfs->UnLimit();
while ( my $cf = $all_cfs->Next ) {
    $cfs{ $cf->Name } = $cf->Id;
}

foreach my $field ( keys %cfs ) {
   
   my @MatchLike = (ref $ARGS{ $field."~" } eq 'ARRAY' ) ? @{ $ARGS{ $field."~" } } : ( $ARGS{$field."~" } );
   my @NoMatchLike = (ref $ARGS{ $field."!~" } eq 'ARRAY' ) ? @{ $ARGS{ $field."!~" } } : ( $ARGS{$field."!~" } );

   my @Match = (ref $ARGS{ $field } eq 'ARRAY' ) ? @{ $ARGS{ $field } } : ( $ARGS{$field } );
   my @NoMatch = (ref $ARGS{ $field."!" } eq 'ARRAY' ) ? @{ $ARGS{ $field."!" } } : ( $ARGS{$field."!" } );

    foreach my $val (@MatchLike) {
        next unless $val;
        push @Match, "~".$val;
    }

    foreach my $val (@NoMatchLike) {
        next unless $val;
        push @NoMatch, "~".$val;
    }


    foreach my $value (@Match) {
        next unless $value;
        my $op;
        if ( $value =~ /^~(.*)$/ ) {
            $value = "%$1%";
            $op    = 'LIKE';
        }
        else {
            $op = '=';
        }
        $articles->LimitCustomField( FIELD           => $cfs{$field},
                                     VALUE           => $value,
                                     ENTRYAGGREGATOR => 'OR',
                                     OPERATOR        => $op );
    }
    foreach my $value (@NoMatch) {
        next unless $value;
        my $op;
        if ( $value =~ /^~(.*)$/ ) {
            $value = "%$1%";
            $op    = 'NOT LIKE';
        }
        else {
            $op = '!=';
        }
        $articles->LimitCustomField( FIELD           => $cfs{$field},
                                     VALUE           => $value,
                                     ENTRYAGGREGATOR => 'OR',
                                     OPERATOR        => $op );
    }
}

### Searches for any custom field

if ($ARGS{'Article~'}) {
    if (ref($ARGS{'Article~'}) eq 'ARRAY') {
	my @Match = @{$ARGS{'Article~'}};
	foreach my $value (@Match) {
	    $articles->LimitCustomField( VALUE => $value,
					 ENTRYAGGREGATOR => 'OR',
					 OPERATOR => 'LIKE' );
	}
    } else {
	$articles->LimitCustomField( VALUE => $ARGS{'Article~'},
				     OPERATOR => 'LIKE' );
    }
}

if ($ARGS{'Article!~'}) {
    if (ref($ARGS{'Article!~'}) eq 'ARRAY') {
	my @NoMatch = @{$ARGS{'Article!~'}};
	foreach my $value (@NoMatch) {
	    $articles->LimitCustomField( VALUE => $value,
					 ENTRYAGGREGATOR => 'OR',
					 OPERATOR => 'NOT LIKE' );
	}
    } else {
	$articles->LimitCustomField( VALUE => $ARGS{'Article!~'},
				     OPERATOR => 'NOT LIKE' );
    }
}

foreach my $field qw(Name Summary Class) {

   my @MatchLike = (ref $ARGS{ $field."~" } eq 'ARRAY' ) ? @{ $ARGS{ $field."~" } } : ( $ARGS{$field."~" } );
   my @NoMatchLike = (ref $ARGS{ $field."!~" } eq 'ARRAY' ) ? @{ $ARGS{ $field."!~" } } : ( $ARGS{$field."!~" } );

   my @Match = (ref $ARGS{ $field } eq 'ARRAY' ) ? @{ $ARGS{ $field } } : ( $ARGS{$field } );
   my @NoMatch = (ref $ARGS{ $field."!" } eq 'ARRAY' ) ? @{ $ARGS{ $field."!" } } : ( $ARGS{$field."!" } );

    foreach my $val (@MatchLike) {
        next unless $val;
        push @Match, "~".$val;
    }

    foreach my $val (@NoMatchLike) {
        next unless $val;
        push @NoMatch, "~".$val;
    }

    my $op;
    foreach my $value (@Match) {
        if ( $value =~ /^~(.*)$/ ) {
            $value = "%$1%";
            $op    = 'LIKE';
        }
        else {
            $op = '=';
        }

        # preprocess Classes, so we can search on class
        if ( $field eq 'Class' && $value ) {
            my $class = RT::FM::Class->new($RT::SystemUser);
            $class->Load($value);
            $value = $class->Id;
        }

        # now that we've pruned the value, get out if it's different.
        next unless $value;

        $articles->Limit( SUBCLAUSE       => $field . 'Match',
                          FIELD           => $field,
                          OPERATOR        => $op,
                          VALUE            => $value,
                          ENTRYAGGREGATOR => 'OR' );

    }
    foreach my $value (@NoMatch) {

        # preprocess Classes, so we can search on class
        if ( $value =~ /^~(.*)/ ) {
            $value = "%$1%";
            $op    = 'NOT LIKE';
        }
        else {
            $op = '!=';
        }
        if ( $field eq 'Class' ) {
            my $class = RT::FM::Class->new($RT::SystemUser);
            $class->Load($value);
            $value = $class->Id;
        }

        # now that we've pruned the value, get out if it's different.
        next unless $value;

        $articles->Limit( SUBCLAUSE       => $field . 'NoMatch',
                          OPERATOR        => $op,
                          VALUE            => $value,
                          FIELD           => $field,
                          ENTRYAGGREGATOR => 'AND' );

    }
}


$m->comp('/Elements/Callback', %ARGS, _Search => $articles);

my %filtered = %ARGS;
delete $filtered{$_} for (@metakeys, "EditTopics", "ExpandTopics");
delete $filtered{$_} for grep {$filtered{$_} !~ /\S/} keys %filtered;
my $QueryString = "?".$m->comp('/Elements/QueryString', %filtered);
</%init>


</%init>
<%ARGS>
$CreatedBefore => ''
$CreatedAfter => ''
$LastUpdatedBefore => ''
$LastUpdatedAfter => ''
$RefersTo => undef
$ReferredToBy => undef
$CurrentSearch => 'new'
</%ARGS>
