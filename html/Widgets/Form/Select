<div id="form-box-<% lc $Name %>">
<% $Description %>:
<select name="Select-<% $Name %>">

% if ( $Empty ) {
% my $selected = '';
% $selected = 'selected' unless @CurrentValue;
<option value="__empty_value__" <% $selected %>><% $EmptyLabel %></option>
% }

% foreach my $v( @Values ) {
% my $selected = '';
% $selected = 'selected' if delete $CurrentValue{ $v };
<option value="<% $v %>" <% $selected %>><% $ValuesLabel{ $v } || $v %></option>
% }

% if ( $Alternative ) {
%     my $selected = '';
%     $selected = 'selected' if keys %CurrentValue;
<option value="__alternative_value__" <% $selected %>><% $AlternativeLabel %></option>
% }

</select>
% if ( $Alternative ) {
<input type="text" class="alternative" name="Select-Alternative-<% $Name %>" value="<% join ', ', @CurrentValue %>" />
% }
</div>
<%INIT>
my %CurrentValue = map {$_ => 1} grep defined, @CurrentValue;
</%INIT>
<%ARGS>
$Name             => '',
$Description      => undef,

@Values           => (),
%ValuesLabel      => (),
@CurrentValue     => (),

$Empty            => 1,
@EmptyValue       => (),
$EmptyLabel       => loc('use default ([_1])', join ', ', @EmptyValue),

$Alternative      => 0,
$AlternativeLabel => loc('other...'),

$Multiple         => 0,
</%ARGS>

<%METHOD Process>
<%ARGS>
$Arguments        => {},

$Name             => undef,

@Values           => (),
%ValuesLabel      => (),
@CurrentValue     => (),

$Empty            => 1,
$Alternative      => 0,
$Multiple         => 0,
</%ARGS>
<%INIT>
my $value = $Arguments->{ "Select-$Name" };
if( !defined $value || $value eq '__empty_value__' ) {
    return [ @CurrentValue ] unless $Empty;
    return undef;
}
$value = [$value] unless ref $value;

if ( $Alternative ) {
    my $alt = $Arguments->{ "Select-Alternative-$Name" };
    if( $Multiple ) {
        push @$value, split /\s*,\s*/, $alt;
    } else {
        push @$value, $alt;
    }
}

splice @$value, 1 unless $Multiple;

# XXX: check values

return $value->[0] unless $Multiple;
return $value;
</%INIT>
</%METHOD>
