<& /Elements/Header, ShowBar => 0 &>
%# From /Elements/Header
</div>
<div id="body">

<a href="#" onclick="window.close(); return false;"><&|/l&>Close window</&></a>

<div class="calendar">
  <table>
    <caption>
      <a class="prev" href="CalPopup.html?DisplayedMonth=<%$prev_month%>&DisplayedYear=<%$prev_year%>&field=<%$field%>"><&|/l&>Prev</&></a>
      <span class="month"><% $months[$DisplayedMonth-1] %> <% $DisplayedYear %></span>
      <a class="next" href="CalPopup.html?DisplayedMonth=<%$next_month%>&DisplayedYear=<%$next_year%>&field=<%$field%>"><&|/l&>Next</&></a>
    </caption>
    <tr>
% foreach my $wday (@weekdays) {
      <th><%$wday%></th>
% }
    </tr>
% foreach my $week (@cal) {
    <tr>
%     foreach my $day (@{$week}) {
      <td>
%         if ($day) {
%             my $datestr = sprintf('%04d-%02d-%02d', $DisplayedYear, $DisplayedMonth, $day);
        <a href="#" onclick="updateParentField('<% $field %>','<% $datestr %>'); return false;"><% $day %></a>
%         } else {
        &nbsp;
%         }
      </td>
%     } #foreach $day
    </tr>
% } # foreach $week
  </table>
</div>

<%init>
use Calendar::Simple;
my @today = localtime(time());

my @weekdays;
push @weekdays, loc($_)
  for qw(Sun Mon Tue Wed Thu Fri Sat);

my @months;
push @months, loc($_)
  for qw(January February March April May June July August
         September October November December);

unless ($DisplayedYear) {
    $DisplayedMonth = $today[4] + 1;
    $DisplayedYear  = ($today[5] + 1900);
}

my ($prev_year, $next_year, $prev_month, $next_month);
$prev_month = $next_month = $DisplayedMonth;
$prev_year  = $next_year  = $DisplayedYear;

$next_month++;
$prev_month--;

if ($DisplayedMonth == 12) {
    $next_year++;
    $next_month = 1;
}
elsif ($DisplayedMonth == 1) {
    $prev_month = 12;
    $prev_year--;
}

my @cal = calendar($DisplayedMonth, $DisplayedYear);
</%init>

<%args>
$field => 'none'
$DisplayedMonth => undef
$DisplayedYear => undef
</%args>
