<select name="<%$Name%>">
%if ($DefaultValue) {
<option value=""<% !$Default && qq[ selected="selected"] |n %>><%$DefaultLabel%></option>
%}
%foreach my $status (@status) {
%next if ($SkipDeleted && $status eq 'deleted');
<option value="<%$status%>"<% (defined $Default && $status eq $Default) && qq[ selected="selected"] |n %>><%loc($status)%></option>
% }
</select>
<%INIT>
### XXX: no cover for Tools/MyDay.html


my @status;
if ( $TicketObj ) {
    my $current = $TicketObj->Status;
    my $lifecycle = $TicketObj->QueueObj->lifecycle;

    my %has = ();
    foreach my $next ( $lifecycle->transitions( $current ) ) {
        my $check = $lifecycle->check_right( $current => $next );
        $has{ $check } = $TicketObj->CurrentUserHasRight( $check )
            unless exists $has{ $check };
        push @status, $next if $has{ $check };
    }
}
elsif ( $QueueObj ) {
    @status = $QueueObj->lifecycle->valid;
}
else {
    @status = RT::Queue->lifecycle->valid;
}
</%INIT>
<%ARGS>
$Name => undef
$TicketObj => undef
$QueueObj => undef
$Default => ''
$SkipDeleted => 0
$DefaultValue => 1
$DefaultLabel => "-"
</%ARGS>
