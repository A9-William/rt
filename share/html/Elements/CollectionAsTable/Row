%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2009 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<%ARGS>
$i => undef
@Format => undef
$record => undef
$maxitems => undef
$Depth => undef
$Warning => undef
$ColumnMap => {}
$Class     => 'RT__Ticket'
</%ARGS>
<%init>
$m->out(  '<tr class="'
        . ( $Warning ? 'warnline' : $i % 2 ? 'oddline' : 'evenline' ) . '" >'
        . "\n" );
use HTML::Entities;
my $item;
foreach my $column (@Format) {
    if ( defined $column->{title} && $column->{title} eq 'NEWLINE' ) {
        while ( $item < $maxitems ) {
            $m->out(qq{<td class="collection-as-table">&nbsp;</td>\n});
            $item++;
        }
        $item = 0;
        $m->out( '</tr>' . "\n" );
        $m->out(  '<tr class="'
                . ( $Warning ? 'warnline' : $i % 2 ? 'oddline' : 'evenline' )
                . '" >'
                . "\n" );
        next;
    }

    my $class = $column->{class} ? encode_entities( $column->{class}, q{'"&<>} ) : 'collection-as-table';
    $m->out(qq{<td class="$class" });

    my $align = $column->{align};
    my $style = $column->{style};
    my $span  = $column->{span};

    $item += ($span || 1);

    my @out;
    foreach my $subcol ( @{ $column->{output} } ) {
        my ($col) = ($subcol =~ /^__(.*?)__$/);
        unless ( $col ) {
            push @out, $subcol;
            next;
        }

        $style = $m->comp(
            "/Elements/ColumnMap",
            Class => $Class,
            Name  => $col,
            Attr  => 'style'
        );
        $align = $m->comp(
            "/Elements/ColumnMap",
            Class => $Class,
            Name  => $col,
            Attr  => 'align'
        );
        unless ( exists $ColumnMap->{$col}{'value'} ) {
            $ColumnMap->{$col} = $m->comp(
                "/Elements/ColumnMap",
                Class => $Class,
                Name  => $col,
                Attr  => 'value'
            );
        }

        push @out, ProcessColumnMapValue(
            $ColumnMap->{$col}{'value'},
            Arguments => [$record, $i]
        );
    }
    s/\n/<br \/>/gs for @out;

    $m->out( 'align="'   . $m->interp->apply_escapes( $align => 'h' ) . '"' )
        if $align;
    $m->out( 'style="'   . $m->interp->apply_escapes( $style => 'h' ) . '"' )
        if $style;
    $m->out( 'colspan="' . $m->interp->apply_escapes( $span  => 'h' ) . '"' )
        if $span;

    $m->out('>');
    $m->out(@out) if @out;
    $m->out( '</td>' . "\n" );
}
$m->out('</tr>');
</%init>
