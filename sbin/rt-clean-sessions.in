#!@PERL@

use strict;
use warnings;
use lib ("@LOCAL_LIB_PATH@", "@RT_LIB_PATH@");

use Getopt::Long;
my %opt;
GetOptions( \%opt, "older=s", "debug", "help");


if ( $opt{help} ) {
    require Pod::Usage;
    import Pod::Usage;
    pod2usage({ -message => "RT Session cleanup tool\n", verbose => 1 });
    exit 1;    
}


if( $opt{'older'} ) {
    unless( $opt{'older'} =~ /^\s*([0-9]+)\s*(H|D|M|Y)?$/i ) {
        print STDERR "wrong format of the 'older' argumnet\n";
        exit(1);
    }
    my ($num,$unit) = ($1, uc($2 ||'D'));
    my %factor = ( H => 60*60 );
    $factor{'D'} = $factor{'H'}*24;
    $factor{'M'} = $factor{'D'}*31;
    $factor{'Y'} = $factor{'D'}*365;
    $opt{'older'} = $num * $factor{ $unit };
}

require RT;
RT::LoadConfig();

if( $opt{'debug'} ) {
    RT->Config->Set( LogToScreen => 'debug' );
} else {
    RT->Config->Set( LogToScreen => undef );
}

RT::ConnectToDatabase();
RT::InitLogging();

require RT::Interface::Web::Session;

if( $opt{'older'} or my $alogoff = int RT->Config->Get('AutoLogoff') ) {
    my $min;
    foreach ($alogoff*60, $opt{'older'}) {
        next unless $_;
        $min = $_ unless $min;
        $min = $_ if $_ < $min;
    }

    RT::Interface::Web::Session->ClearOld( $min );
}

RT::Interface::Web::Session->ClearByUser;

exit(0);

__END__

=head1 NAME

rt-clean-sessions - clean old and duplicate RT sessions

=head1 SYNOPSIS

    rt-clean-sessions [--debug] [--older <NUM>[H|D|M|Y]]

    rt-clean sessions
    rt-clean sessions --debug
    rt-clean sessions --older 10D
    rt-clean sessions --debug --older 1M

=head1 DESCRIPTION

Script cleans RT sessions from DB or dir with sessions data.
Leaves in DB only one session per RT user and sessions that aren't older
than specified(see options).

Script is safe because data in the sessions is temporary and can be deleted.

=head1 OPTIONS

=head2 older

Date interval in the C<< <NUM>[<unit>] >> format. Default unit is D(ays),
H(our), M(onth) and Y(ear) are also supported.

For exmaple: C<rt-clean sessions --older 1M> would delete all sessions that are
older than 1 month.

=head2 debug

Turn on debug output.

=cut
